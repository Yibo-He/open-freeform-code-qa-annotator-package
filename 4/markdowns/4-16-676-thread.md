
# Post \#71384840 [Link](https://stackoverflow.com/questions/71384840/)

## How to execute Powershell's "start-process -Verb RunAs" from inside a Batch where the elevated command inherits the Batch's environment?

**Vote**: 3 (575/702) **Views**: 3051 (568/702) 

**Internal ID** \#4-16-676

Created at 2022-03-07 17:13:33

Tags: `powershell` `batch-file` `cmd` `admin` `elevation`

----------

#### Metadata:

Area: `IT Operations`

Language: `bash` (full parsed tag list: `bash`)

----------

**Notepad**


----------


## 1. Problem


I have a complicated batch file where some parts need to run with elevated/admin rights (e.g. interacting with Windows services) and I found a Powershell way to do that:
```
powershell.exe -command "try {$proc = start-process -wait -Verb runas -filepath '%~nx0' -ArgumentList '<arguments>'; exit $proc.ExitCode} catch {write-host $Error; exit -10}"
```

But there's a huge caveat! The elevated instance of my script (`%~nx0`) starts with a fresh copy of environment variables and everything I `set "var=content"` before is unavailable.

## 2. What I've tried so far


This Powershell script doesn't help either because `Verb = "RunAs"` requires `UseShellExecute = $true` which in turn is mutually exclusive to/with `StartInfo.EnvironmentVariables.Add()`
```
$p = New-Object System.Diagnostics.Process
$p.StartInfo.FileName = "cmd.exe";
$p.StartInfo.Arguments = '/k set blasfg'
$p.StartInfo.UseShellExecute = $true;
$p.StartInfo.Verb = "RunAs";
$p.StartInfo.EnvironmentVariables.Add("blasfg", "C:\\Temp")

$p.Start() | Out-Null
$p.WaitForExit()
exit $p.ExitCode
```

And even if that would work I'd still need to  dozens of variables...

## 3. unappealing semi-solutions




1. helper tools like hstart - because I can't relay on external tools. Only CMD, Powershell and maybe VBscript (but it looks like runas plus wait and errorlevel/ExitCode processing isn't possible with/in vbs).
2. passing (only required) variables as arguments - because I need dozens and escaping them is an ugly chore (both the result and doing it).
3. restarting the whole script - because it's inefficient with all the parsing, checking processing and other tasks happening again (and again and ...). I'd like to keep the elevated parts to a minimum and some actions can later be run as a normal user (e.g service start/stop).
4. Writing the environment to a file and rereading it in the elevated instance - because it's an ugly hack and I'd hope there's a cleaner option out there. And writing possibly sensitive information to a file is even worse than storing it temporarily in an environment variable.




----------
        
## Answer \#0

**Accepted** Vote: 2

Created at 2022-03-07 19:09:43

------------

Here's a proof of concept that uses the following :
- Make the `powershell` call invoke , aux. `powershell` instance as the elevated target process.- This allows the outer `powershell` instance to "bake" `Set-Item` statements that re-create the caller's environment variables (which the outer instance inherited, and which can therefore be enumerated with `Get-ChilItem Env:`) into the  `-command` string passed to the aux. instance, followed by a re-invocation of the original batch file.
: This solution blindly recreates  environment variables defined in the caller's process in the elevated process - consider pre-filtering, possibly by name patterns, such as by a shared prefix; e.g., to limit variable re-creation to those whose names start with `foo`, replace `Get-ChildItem Env:` with `Get-ChildItem Env:foo*` in the command below.
```
@echo off & setlocal

:: Test if elevated.
net session 1>NUL 2>NUL && goto :ELEVATED 

:: Set sample env. vars. to pass to the elevated re-invocation.
set foo1=bar
set "foo2=none      done"
set foo3=3" of snow
:: " dummy comment to fix syntax highlighting
:: Helper variable to facilitate re-invocation.
set "thisBatchFilePath=%~f0"

:: Re-invoke with elevation, synchronously, reporting the exit
:: code of the elevated run.
:: Two sample arguments, ... and "quoted argument" are passed on re-invocation.
powershell -noprofile -command ^
  trap { [Console]::Error.WriteLine($_); exit -667 } ^
  exit ( ^
    Start-Process -Wait -PassThru -Verb RunAs powershell ^
      "\" -noprofile -command `\" $(Get-ChildItem Env: | ForEach-Object { 'Set-Item \\\"env:' + $_.Name + '\\\" \\\"' + $($_.Value -replace '\""', '`\\\""') + '\\\"; ' }) cmd /c '\`\"%thisBatchFilePath:'=''%\`\" ... \`\"quoted argument\`\" & exit'; exit `$LASTEXITCODE`\" \"" ^
  ).ExitCode 

echo -- Elevated re-invocation exited with %ERRORLEVEL%.

:: End of non-elevated part.
exit /b

:ELEVATED

echo Now running elevated...

echo -- Arguments received:
echo [%*]

echo -- Env. vars. whose names start with "foo":
set foo 

:: Determine the exit code to report.
set ec=5

echo -- Exiting with exit code %ec%...
:: Pause, so you can inspect the output before exiting.
pause
exit /b %ec%
```

Note:
- `trap { [Console]::Error.WriteLine($_); exit -667 }` handles the case where the user declines the elevation prompt, which causes a statement-terminating error that the [trap](https://learn.microsoft.com/en-us/powershell/module/microsoft.powershell.core/about/about_Trap) statement catches (using a [try / catch](https://learn.microsoft.com/en-us/powershell/module/microsoft.powershell.core/about/about_Try_Catch_Finally) statement around the `Start-Process` call is also an option, and usually the better choice, but in this case `trap` is syntactically easier).- Specifying pass-through arguments (arguments to pass directly to the re-invocation of the (elevated) batch file, after the `cmd /c '\`\"%thisBatchFilePath:'=''%\`\"` part above):- `'``''`- `'\`\"...\`\"``\`\"quoted argument\`\"`- The `cmd /c '<batch-file> & exit'` re-invocation technique is required to ensure  exit-code reporting, unfortunately - see [this answer](https://stackoverflow.com/a/55290133/45375) for details.- The explicit `exit $LASTEXITCODE` statement after the batch-file re-invocation is required to make the PowerShell CLI report the specific exit code reported by the batch file - without that,  exit code would be mapped to `1`. See [this answer](https://stackoverflow.com/a/57468523/45375) for a comprehensive discussion of exit codes in PowerShell.


------------
    
    
## Answer \#1

 Vote: 2

Created at 2022-03-16 06:44:47

------------


## A piping hot solution


Derived from [mklement0](/a/71386103/4323903)'s working proof of concept and  Base64 suggestion I've built a solution with this approach:
- - - - 
It is more flexible because you're not limited to environment variables (you can essentially pass on anything (text based)) and the Powershell command doesn't need to be edited to choose what gets piped through. But it has a few limitations mklement0's implementation doesn't suffer from:
- `barz`- `fooDoubleQouting`- 

### Example / test batch:


```
@echo off & setlocal EnableDelayedExpansion

::# Test if elevated.
net session 1>NUL 2>NUL && goto :ELEVATED

(set LF=^
%=this line is empty=%
)
::# Set sample env. vars. to pass to the elevated instance.
set foo1=bar
set "foo2=none done"
set foo3=3" of snow
set "barz=  Line1!LF!  foo1=Surprise^! foo1 isn't '%foo1%' anymore. It was unintentionally overwritten."
set barts=1 " 3_consecutive_" "_before_EOL   
set "barl=' sdfs' ´´`` =43::523; \/{[]} 457wb457;; %%%^!2^!11^!^!"
::# ' dummy comment#1 to fix syntax highlighting.

::# Helper variable to facilitate re-invocation (in case %~f0 contains any single quotes).
set "_selfBat=%~f0"

::# DDE - so "!" don't get expanded anymore. Was only needed for "set barz=..."
setlocal DisableDelayedExpansion
::# print variables etc. to console before self invocation & elevation.
call :testPrint

::# Generate pipe input. Be aware of CMD's handicaps of whats allowed in a command block.
::# eg. "REM" is not allowed and neither is echoing an unescaped closing parenthesis: ")" -> "^^^)"
(
    echo[foo_Setting_one=extra-varval.
    set ^^"
    echo[bar_stuff=in between.^^^)^^^"
    set bar
    echo["fooDoubleQouting=testertest"
) | powershell.exe -nologo -noprofile -command ^
    trap { [Console]::Error.WriteLine($_); exit -667 } ^
    exit ( ^
        Start-Process -PassThru -Wait -WindowStyle Maximized -Verb RunAs 'powershell.exe' ^
            "\"-nol -nop -comm `\" $('Write-Output $([Text.Encoding]::UTF8.GetString([System.Convert]::FromBase64String(\\\"' + $([Convert]::ToBase64String([Text.Encoding]::UTF8.GetBytes($(foreach ($i in $input) {\"$i`n\"})))) + '\\\")))') | cmd.exe '/D','/U','/T:4F','/S','/C',' \`\"%_selfBat:'=''%\`\" \`\"quoted argument\`\" nonQtdArg & exit'; exit `$LastExitCode `\" \"" ^
    ).exitCode

echo[
echo[ ---- Returned errorlevel is: %ERRORLEVEL%
pause
endlocal & endlocal & exit /b %ERRORLEVEL%


:testPrint
    echo[
    echo[ ---- WhiteSpaceTest: "%barts%"
    set foo
    set bar
    echo[
    set ^"
exit /B
::# " dummy comment#2 to fix syntax highlighting again.

:ELEVATED
setlocal DisableDelayedExpansion
    ::# Read and parse piped in data.
    ::# (with "delims" & "eol" truly defined as empty so every line is read as-is, even empty lines)
    for /F delims^=^ eol^= %%A in ('findstr.exe "^"') do (
        echo[ Parsing %%~A
        for /F "tokens=1,* delims=="eol^= %%B in ("%%~A") do (
            echo[   into "%%~B"
            echo[     equals "%%~C"
            ::# Convert the piped in data back into environment variables+values.
            set "%%~B=%%~C" 2>NUL
        )
        echo[
    )
    echo[-------- END PIPEREADING --------
    echo[-- Arguments received:
    echo[ [%*]

    call :testPrint

    set "ERR=42"
    echo[
    ::# to actually pause and/or wait for / react to user input(!) one needs to pipe in CON (console).
    <con set /P ERR=Enter arbitrary exitcode / errorlevel: 
endlocal & exit /B %ERR%
```


### Notes:


- [mklement0](/a/71386103/4323903)- `CMD /C '<batch-file_withEscaped'> & exit'``exit /b X``&\`\"%_selfBat%\`\"``CMD /C ... & exit``'arg1','arg2'`- `'/D','/T:4F',`- `echo[``echo``echo.*`- `<con``pause``set /P ...``<con``nul``con``break`[from in here](https://www.dostips.com/forum/viewtopic.php?t=8940)- `barl`

## Escaping hell


Here are the dynamic middle and inner  to show whats going on and shave away some escaping magic:
```
powershell.exe -nol -nop -comm "Write-Output $([Text.Encoding]::UTF8.GetString([System.Convert]::FromBase64String(\"<<BASE64_BLOB>>\"))) | cmd.exe '/D','/U','/T:4F','/S','/C',' \"<<path\this.cmd_withEscaped'>>\" \"quoted argument\" nonQtdArg & exit'; exit $LastExitCode"
```

Even with just my default environment that command line is somewhere around 5kB big(!), thanks to the `<<BASE64_BLOB>>`.
```
cmd.exe /D /U /T:4F /S /C " "<<path\this.cmd_withNormal'>>" "quoted argument" nonQtdArg & exit"
```



------------
    
    
## Answer \#2

 Vote: 1

Created at 2022-04-21 05:17:36

------------

So yeah, like has been said, the environment is not meant to be passed from a user to another, by design, because of the security implications. It doesn't mean that it can't be done, even if it's not something you're "supposed" to do. While I do think you should look into what do you actually want to achieve, I absolutely hate the type of answers where people tell you what you  "should" do and not answer the actual question at all. So I'm giving you both options here.

#### "Passing" the environment


You have several options here

1. From the elevated child process, read the environment variables from the unelevated caller parent process' memory using the NtQueryInformationProcess and ReadProcessMemory APIs. Then either overwrite the variables on the target process (in your case, the current process) with WriteProcessMemory or just set them as you normally would. You can achieve this with only Powershell, albeit you need to add some C# code to call the required API functions. Here in my Enable-Privilege.ps1 example you can see how to implement NtQueryInformationProcess in PowerShell and manipulate a parent process (I wrote it for the purpose of modifying privilege tokens inside self/parent/any process). You would actually want to use a part of it because you need to enable SeDebugPrivilege to be able to manipulate memory of other processes. This option would probably be most "clean" and robust solution without any instantly obvious caveats. See this codeproject article for more information: Read Environment Strings of Remote Process
2. Inside the unelevated parent process, iterate through all the environment variables and write them as a string to a single environment variable. Then pass that single environment value as an argument when spawning the elevated child process, where you can parse that string and write those environment values back. You would likely run to the same caveats as option 3 here though.
3. Pipe the variables from the parent to the child, like has been proposed here already. The problem here is that the batch processor is really finicky and the rules of parsing and escaping are super janky, so it's very likely you would run to issues with special characters and other similar caveats with this option.
4. Using a kernel-mode driver, overwrite the security token of the unelevated process with a elevated one, writing back the original token after you are done. On the surface this would seem like the perfect solution, since you could actually stay inside the previously-unelevated process and retain it's environment without changing context, only the security context would be replaced. As in kernel-mode you can modify everything, the security tokens are simple memory structs inside kernel memory which you can change. The problem with this approach is that it completely bypasses the windows security model, as it's supposed to be impossible to change the security token of an existing process. Because it's supposed to be "impossible", it goes deep into undocumented territory and inside the kernel you can easily break stuff if you don't know what you're doing, so this is definitely the "advanced" option (even though this particular thing is not too complicated, it's basically just writing some memory). As it's something you're not supposed to be doing, there is a possibility it breaks something since Windows does not expect a process to suddenly have a different security context. That being said, I've used this approach with no problems in the past. It could be broken in the future though by any change in the security design. You would also need to either enable testsigning (aka Disable Driver Signature Enforcement), digitally sign your driver or use some other method to bypass this requirement (f.ex. through a hypervisor or an exploit), but that is out of the scope of this answer.



### The achsually version



In this case, I would do exactly that. Since your problem is of such nature that a easy solution for it doesn't exist, because it's not supported by design. It's hard to propose a specific solution since the lack of information of what it is you're actually trying to achieve here.
I'm gonna try to cover this in a general sense. First is to think about the  part. What are the operations you need to do which require elevation? There would be multiple ways to achieve whatever it is in a supported fashion.
Examples:
- For whatever you need to read/write/modify, you could change the security security settings of the  (instead of the ). Meaning that let's say you need to access a specific registry key, service, file, folder, whatever, you could simply modify the ACL of the  to allow the  (i.e. the user) to do whatever operation you need. If you need to modify a single service for example, you could add the start/stop/modify right for only that single process.- If the thing you need is specific to the types of operations rather than specific targets, you could add the required [privileges](https://learn.microsoft.com/en-us/windows/win32/secauthz/privilege-constants) to the "Users" group. Or make a new group with the required privileges, and then add the user to that group.- If you want more granular control on what can/can't be done and/or the operations are specific, you could write a simple program and run it as a elevated service. Then you could just tell that service to do the required operations from the unelevated batch script, so no requesting elevation and spawning new process would be needed. You could simply do `my-service.exe do-the-thing` from batch, and that my-service would do the operation you need.- You could also always ask for the elevation in beginning of the script, but as it's clear you don't want to do this with full administrator rights, you could create a new user for just this purpose which you add to a new group for it which has the required privileges you need. Note that without resorting to the aforementioned kernel-mode ""hacks"", you cannot  new privileges for a user on-the-fly, only // existing ones. What you can do though is add them beforehand based on what you need, but that will need to happen before the process is started.


------------
    
    